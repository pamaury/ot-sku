# Copyright lowRISC contributors (OpenTitan project).
# Licensed under the Apache License, Version 2.0, see LICENSE for details.
# SPDX-License-Identifier: Apache-2.0

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

def _hub_repo_impl(rctx):
    for folder_name, spoke_file in rctx.attr.repo_mapping.items():
        # Get the absolute path to the spoke repository's root
        # using the label of a file in the root of the spoke repository.
        spoke_path = rctx.path(spoke_file).dirname

        # Symlink the entire spoke repo to a top-level folder in the hub
        rctx.symlink(spoke_path, folder_name)

    # Create a root BUILD file so Bazel recognizes these as packages
    rctx.file("BUILD.bazel", "# Hub Root")

hub_repo = repository_rule(
    implementation = _hub_repo_impl,
    attrs = {"repo_mapping": attr.string_keyed_label_dict(
        doc = "Map hub symlink names to spoke repositories.  The spoke repo should be the label of a file in the root of the spoke repo (e.g. BUILD.bazel)",
    )},
)

_ARCHIVES = {
    "presign_perso": {
        "url": "https://github.com/pamaury/ot-sku/releases/download/TEST_RELEASE_2/presign_perso.tar.xz",
        "sha256": "c2241a0f3df8469d67afe2ea39de2bc64d56e63304a68179eba5d64ea6d3c360"
    },
    "presign_rom_ext": {
        "url": "https://github.com/pamaury/ot-sku/releases/download/TEST_RELEASE_2/presign_rom_ext.tar.xz",
        "sha256": "027d9b65e3111cb0160680c0ea5492506b70c3a5e70cb7cdb576a104e200793c"
    },
    "perso_release": {
        "url": "https://github.com/pamaury/ot-sku/releases/download/TEST_RELEASE_2/perso_release.tar.xz",
        "sha256": "4bdfa5dfa2aa5dec697e843a0a6a56b62959caa000a70c0c081d2046dbc6f099"
    },
    "rom_ext_release": {
        "url": "https://github.com/pamaury/ot-sku/releases/download/TEST_RELEASE_2/rom_ext_release.tar.xz",
        "sha256": "8207d5bba743f56d13f64ed3d1ac3085e428c2ffde47eb3d40bbe9f702b6e584"
    }
}

def _extra_impl(mctx):
    for (name, info) in _ARCHIVES.items():
        http_archive(
            name = name,
            **info
        )
    hub_repo(
        name = "provisioning_exts_extra",
        repo_mapping = {
            name: "@{}//:BUILD.bazel".format(name)
            for name in _ARCHIVES
        },
    )

extra = module_extension(
    implementation = _extra_impl,
)
